configure(allprojects) { project ->
    group = 'party.threebody.skean'
    ext.versions = [
            //lang
            java                  : '1.8',
            groovy                : '2.4.11',
            servlet               : '4.0.0',

            //apache commons
            'commons-lang'        : '2.6',
            'commons-lang3'       : '3.5',
            'commons-collections' : '3.2.2',
            'commons-collections4': '4.1',
            'commons-io'          : '2.5',
            'commons-logging'     : '1.2',
            'commons-beanutils'   : '1.9.3',
            'commons-codec'       : '1.10',
            'commons-net'         : '3.6',

            //logging
            'log4j'               : '1.2.17',
            'log4j2'              : '2.8.2',
            'logback'             : '1.2.3',
            'slf4j'               : '1.7.25',

            //spring
            'spring'              : '5.0.0.RELEASE',
            'spring-security'     : '5.0.0.M3',
            'spring-boot'         : '2.0.0.M4',

            //others
            'guava'               : '21.0',
            'jackson'             : '2.9.1',
            'ehcache'             : '3.3.1',
            'junit'               : '4.12',

            //data
            'mariadb-java-client' : '2.1.1',
            'HikariCP'            : '2.7.1',
            'h2'                  : '1.4.196'
    ]

    repositories {
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url 'http://repo.spring.io/milestone/' }
        mavenCentral()
        jcenter()
    }

    apply plugin: 'jacoco'
}


configure(subprojects) { project ->
    dependencies {

    }

    apply plugin: 'java'
    apply plugin: 'idea'
    compileJava {
        sourceCompatibility = versions.java
        targetCompatibility = versions.java
        options.encoding = 'UTF-8'
        options.compilerArgs += '-parameters'
    }

    compileTestJava {
        [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'
        sourceCompatibility = versions.java
        targetCompatibility = versions.java
        options.encoding = 'UTF-8'
        options.compilerArgs += "-parameters"
    }

    apply plugin: 'maven-publish'

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourceJar {
                    classifier "sources"
                }
            }
        }
    }

    if (project.plugins.hasPlugin('groovy')) {
        compileGroovy {
            options.encoding = 'UTF-8'
        }
        compileTestGroovy {
            options.encoding = 'UTF-8'
        }
    }

    jacocoTestReport {
        additionalSourceDirs = files(sourceSets.main.allSource.srcDirs)
        sourceDirectories = files(sourceSets.main.allSource.srcDirs)
        classDirectories = files(sourceSets.main.output)
        reports {
            html.enabled = true
            xml.enabled = true
            csv.enabled = false
        }
    }


}
// see https://gist.github.com/aalmiray/e6f54aa4b3803be0bcac
// see https://docs.gradle.org/current/userguide/jacoco_plugin.html
// see https://docs.gradle.org/current/dsl/org.gradle.testing.jacoco.tasks.JacocoReport.html
task rootJacocoReport(type: JacocoReport) {
    dependsOn = subprojects.test
    additionalSourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories = files(subprojects.sourceSets.main.output)
    def executionData0 = files(subprojects.jacocoTestReport.executionData)
    executionData = files(executionData0.findAll { it.exists()})

    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }
    onlyIf = {
        true
    }

}

// see: https://goo.gl/gthmv8
task rootTestReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    reportOn subprojects.collect { it.tasks.withType(Test) }
}